plugins {
    id 'scl.java'
    id 'scl.spring-cloud'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

swaggerSources {
  def typeMappings = 'URI=URI'
  def importMappings = 'URI=java.net.URI'
  Currency {
    def apiYaml = "${projectDir}/src/main/resources/api/Currency.yaml"
    def configJson = "${projectDir}/src/main/resources/api/config.json"
    inputFile = file(apiYaml)
    def ignoreFile = file("${projectDir}/src/main/resources/api/.openapi-generator-ignore")
    code {
        // See: https://openapi-generator.tech/docs/generators/spring/
        language = 'spring'
        configFile = file(configJson)
        rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings', typeMappings, '--import-mappings', importMappings] as List<String>
        components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
    }
  }
}

compileJava.dependsOn swaggerSources.Currency.code
sourceSets.main.java.srcDir "${swaggerSources.Currency.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.Currency.code.outputDir}/src/main/resources"

dependencies {
    // OpenAPI Starts
    swaggerCodegen 'org.openapitools:openapi-generator-cli:6.2.1'
    compileOnly 'io.swagger.core.v3:swagger-annotations:2.2.7'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.4'
    compileOnly 'javax.xml.bind:jaxb-api:2.3.1'
    compileOnly 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    compileOnly 'org.springframework.boot:spring-boot-starter-hateoas'
    compileOnly 'org.springframework.cloud:spring-cloud-starter-openfeign'
    // OpenAPI Ends
}
